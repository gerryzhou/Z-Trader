
using elsystem;
using elsystem.drawingobjects;
using elsystem.drawing;

inputs:
	double LineValue( Close ) [
		DisplayName = "LineValue", 
		ToolTip = "Enter an EasyLanguage expression.  Enter the value at which to plot the horizontal line."],
	int RiskAmt (10) [ DisplayName = "RiskAmt",
		ToolTip = "The $ amount risk at each trade." ],
	int CapAmt (20000) [ DisplayName = "CapAmt",
		ToolTip = "The total $ amount at each trade." ],
	double PriceRatioLess (20) [ DisplayName = "PriceRatioLess",
		ToolTip = "The ratio for price calculation from the Today's spread." ],
	double PriceRatioMid (30) [ DisplayName = "PriceRatioMid",
		ToolTip = "The ratio for price calculation from the Today's spread." ],
	double PriceRatioMore (45) [ DisplayName = "PriceRatioMore",
		ToolTip = "The ratio for price calculation from the Today's spread." ],
	int DataNum (9) [ DisplayName = "DataNum",
		ToolTip = "The amount of data series added to this chart." ],
	int TimeOpen (705) [ DisplayName = "TimeOpen",
		ToolTip = "The time of market open." ],
	int TimeStartTrade (630) [ DisplayName = "TimeStartTrade",
		ToolTip = "The time to start trade." ],
	int TimeShowText (530) [ DisplayName = "TimeStartTrade",
		ToolTip = "The time to start trade." ],
	bool ShowMarketOpen (true) [ DisplayName = "ShowMarketOpen",
		ToolTip = "Show the session open bar/range." ],
	int RocLength (8) [ DisplayName = "RocLength",
		ToolTip = "Rate of Change lookback length." ],
	int RocMALength (8) [ DisplayName = "RocMALength",
		ToolTip = "Rate of Change MA length." ],
	int HoursKeyRevLookback (1) [ DisplayName = "HoursKeyRevLookback",
		ToolTip = "How many hours lookback to detect Key reversals." ],
	int KeyRevLengthPerH( 12 ) [DisplayName = "KeyRevLengthPerH", ToolTip = 
	 "Enter number of bars per hour over which to calculate key veversal HiLo for PctSpread."],

	int LineColor( Yellow ) [
		DisplayName = "LineColor", 
		ToolTip = "Enter the color to use for the horizontal line."],
	int LineColorHi( Red ) [
		DisplayName = "LineColorHi", 
		ToolTip = "Enter the color to use for the horizontal line."],
	int LineColorLo( Green ) [
		DisplayName = "LineColorLo", 
		ToolTip = "Enter the color to use for the horizontal line."],				
	
	string LineStyle( "dotted" ) [ //"solid"
		DisplayName = "LineStyle", 
		ToolTip = "Enter the style to use for the horizontal line.  Example:  'dashed'"],
	
	int LineWeight( 0 ) [
		DisplayName = "LineWeight", 
		ToolTip = "Enter a number from 0 to 6."],
	
	double PercentTransparency( 50 ) [
		DisplayName = "PercentTransparency", 
		ToolTip = "Enter a number from 0 to 100.  0 is fully opaque.  100 is fully transparent."],
		
	int LineLabelLeftOrRightJustified( 0 ) [
		DisplayName = "LineLabelLeftOrRightJustified", 
		ToolTip = "Enter 0 to show the label on the left side of the horizontal line.  Enter 1 to show the label on the right side of the horizontal line."],	
	
	int HideOrShowLineLabel( 1 ) [
		DisplayName = "HideOrShowLineLabel", 
		ToolTip = "Enter 1 to show the label on the horizontal line.  Enter 0 to not show the label."],	
	
	double TextFontSize( 9 ) [
		DisplayName = "TextFontSize", 
		ToolTip = "Set to the point size of the desired font. Example: Set to 10 for a 10 point font."];
	 
variables:
	HorizontalLine HorizLine( NULL ),
	HorizontalLine HorizLineHi( NULL ),
	HorizontalLine HorizLineLo( NULL ),
	TextLabel TxtRegMktOpenGapR( NULL ),
	double GapR(0),
	double OpenRngR(0), {OpenBarRng/KeyRevSpread}
	double BarRng(0),
	double KeyRevSpread(0),
	KeyRevLoBar(-1),
	KeyRevHiBar(-1),
	KeyRevLoVal(0),
	KeyRevHiVal(0),
	double CloseDS(0),
	double HiDS(0),
	double LoDS(0),
	int SharesToTrade(0);
		
method HorizontalLine CreateHorizontalLine(int LnColor)
variables:  HorizontalLine HL;
begin
	HL = new HorizontalLine();
	HL.Color = GetColorFromIntAndTransparency( LnColor, PercentTransparency );
	HL.Style = GetHLStyleType( LineStyle );
	HL.Weight = LineWeight;
			
	{ lock the horizontal line to prevent inadvertent dragging by the mouse }
	HL.Lock = true;
	
	if TextFontSize > 0 then
		HL.Label.Font = Font.Create( HL.Label.Font.Name, TextFontSize );

	
	HL.RightJustify = LineLabelLeftOrRightJustified = 1;
	HL.ShowLabels = HideOrShowLineLabel = 1;
	HL.ShowInPlotSubgraph = false;

	{
	To have a drawing object persist (i.e., stay on the chart once drawn), the 
	drawing object either 1) must be configured to persist by setting the Persist
	property to true, or 2) the drawing object must be drawn on a bar closing tick;
	in this indicator, the Horizontal line is configured to persist by setting
	the "Persist" property to true.  This is important since the horizontal line is
	only created once (i.e., this method is only called once).
	}
	HL.Persist = true;
	{  
	this is how the horizontal line is "shown" - the horizontal line is added to the  
	DrawingObjects collection; if you want to remove the horizontal line, you can  
	use the	Delete method of the DrawingObjects class; note that the DrawingObjects
	collection is NULL in RadarScreen - even though this study is only enabled for
	Charting, this check was included as a coding example to reinforce the checking
	before adding the drawing object to the DrawingObjects collection
	} 
	if DrawingObjects <> NULL then
	begin
		DrawingObjects.Add( HL );
	end;	
	return HL;
end;

method Color GetColorFromIntAndTransparency( int pColor, double pPctTransp )
variables:  int AlphaValue, double TransPct, Color tempColor;
begin
	{ the percent transparency is only allowed to be between 0 and 100 }
	if pPctTransp > 100 then
		TransPct = 100
	else if pPctTransp < 0 then
		TransPct = 0
	else
		TransPct = pPctTransp;
	
	{
	the following code is needed to convert percentage transparency to
	a number between 0 and 255; where 255 is fully opaque and 0 is fully 
	transparent
	}
	AlphaValue = Round( 0.01 * ( 100 - TransPct ) * 255, 0 ) 
	 astype int;
	
	tempColor = Color.FromARGB( AlphaValue, GetRValue( pColor ), 
	 GetGValue( pColor ), GetBValue( pColor ) );

	return tempColor;
end;

method StyleType GetHLStyleType( string HorizontalLineStyle )
variables: StyleType HLStyleType;
begin
	switch ( LowerStr( HorizontalLineStyle ) )
	begin
		case "dashed":
			HLStyleType = StyleType.dashed;
		case "dashed2":
			HLStyleType = StyleType.dashed2;
		case "dashed3":
			HLStyleType = StyleType.dashed3;
		case "dotted":
			HLStyleType = StyleType.dotted;
		default:
			HLStyleType = StyleType.solid;	
	end;
	
	return HLStyleType;
end;

method void MoveHorizLine( HorizontalLine HorizLineToMove, double HorizLinePrice )
begin
	HorizLineToMove.Price = HorizLinePrice;
end;

method void UpdateHorizLines()
begin
	MoveHorizLine( HorizLineHi, H );
	MoveHorizLine( HorizLineLo, L );
	MoveHorizLine( HorizLine, MedianPrice );
	//MoveHorizLine( HorizLine, LineValue );	
end;

method void UpdateText()
begin
	MoveHorizLine( HorizLineHi, H );
	MoveHorizLine( HorizLineLo, L );
	MoveHorizLine( HorizLine, MedianPrice );
	//MoveHorizLine( HorizLine, LineValue );
end;

method int GetKeyRevSpread()
variables: int barsBack;
begin
	barsBack = HoursKeyRevLookback*KeyRevLengthPerH;
	KeyRevHiVal = 0;
	KeyRevHiBar = -1;
	KeyRevLoVal = 0;
	KeyRevLoBar = -1;
	KeyRevSpread = 0;
	if 1 = Extremes(High, barsBack, 1, KeyRevHiVal, KeyRevHiBar) And
	 	1 = Extremes(Low, barsBack, -1, KeyRevLoVal, KeyRevLoBar) Then
	Begin
		if KeyRevHiVal > 0 and KeyRevLoVal > 0 Then
			KeyRevSpread = KeyRevHiVal - KeyRevLoVal;
{ Add plot in readme will crash the system		if KeyRevHiBar >= 0 Then
			Plot1[KeyRevHiBar]( High, !( "KeyRevHi" ) ) ;}
	End;
	return -1;
end;

Method double GetOpenRngPercent(int dataN, int BarsBack, out string oSymbol,
 out double oPctChg, out double oPctSpd, out double oRocMa, out double oRngR, out double oWickRD, out double oRngRD, out double oWickR)
Variables:
double PrcClose, double LastDClose, double PrcHi, double PrcLo,
double HiD, double LoD, double SpdD, double trueRng;
Begin
	oSymbol = Symbol of Data(dataN);
	PrcClose = C[BarsBack] of Data(dataN);
	PrcHi = H of Data(dataN);
	PrcLo = L of Data(dataN);
	
	HiD = HighD(0)[1] of Data(dataN);
	LoD = LowD(0)[1] of Data(dataN);
	SpdD = HiD - LoD;
	LastDClose = CloseD(1) of Data(dataN);
	oRocMa = GF_RocMA(C, RocLength, RocMALength) of Data(dataN);
	if oRocMa > 0 then
		trueRng = (C[1] - L) of data(dataN)
	else if oRocMa < 0 Then
		trueRng = (H - C[1]) of data(dataN)
	Else
		trueRng = 0;
	if LastDClose <> 0 and PrcClose <> 0 and (HiD - LoD <> 0) then Begin
		oPctChg = Round(Absvalue(100*(PrcClose - LastDClose)/LastDClose), 2);
		oPctSpd = Round(Absvalue(100*SpdD/LastDClose), 2);
		oWickRD = Round(100*trueRng/SpdD, 2);		
		oRngRD = Round(100*(PrcHi - PrcLo)/SpdD, 2);
		
		oRngR = Round(100*(PrcHi - PrcLo)/PrcClose, 2);
		oWickR = Round(100*trueRng/PrcClose, 2);
		
		Print(oSymbol, "		", PrcHi, "	", PrcLo, "	", PrcClose, "	", HiD, "	", LoD, "		",
		  oPctChg, "	", oPctSpd, "	", oRocMa, "	", oWickRD, "	", oRngRD, "	", oRngR, "	", oWickR);
		End
	Else Begin
		oRngR = 0;
		oRngRD = 0;
	end;
	return 1;
end;

{Sort: max Long, max short, min Long, min Short;
related: %chg/LastDClose}
method void GetOpenRngKeyRevSpdRatio()
vars: string sym,  double ret, int dnum,
double pctChg, double pctSpd, double rocMa, double rngRb, double wickRd, double rngRd, double wickR;
begin
	if BarRng <> 0 Then
		Begin
			Print("Symb		_High	__Low	Close	HighD	_LowD		PtChg	PtSpd	RocMa	WikRD	RngRD	_RngR	WickR");
			For dnum = 1 To DataNum Begin
				ret = GetOpenRngPercent(dnum, 1, sym, pctChg, pctSpd, rocMa, rngRb, wickrd, rngRd, wickR); //Round(BarRng/KeyRevSpread, 2)
			End;
		end;
end;

method int GetEnExitPriceDataN(int dataN, int BarsBack, out string oSymbol, out double oPrcOri,
out double oHiD, out double oLoD, out double oSpdD, out double oPctChg, out double oPctSpd, out double oRocMa,
out double oPrcLess, out double oPrcMid, out double oPrcMore)
variables: string txtLbl, double LastDClose;
begin
	oSymbol = Symbol of Data(dataN); 
	oPrcOri = Close of Data(dataN);
	oHiD = HighD(0) of Data(dataN);
	oLoD = LowD(0) of Data(dataN);
	LastDClose = CloseD(1) of Data(dataN);
	oSpdD = oHiD - oLoD;
	oRocMa = GF_RocMA(C, RocLength, RocMALength) of Data(dataN);
	if LastDClose <> 0 and oSpdD <> 0 then Begin
		oPctChg = Round(Absvalue(100*(oPrcOri - LastDClose)/LastDClose), 2);
		oPctSpd = Round(100*oSpdD/LastDClose, 2);
	End;
	if oRocMa > 0 Then Begin
		oPrcLess = oPrcOri - PriceRatioLess*oSpdD/100;
		oPrcMid = oPrcOri - PriceRatioMid*oSpdD/100;
		oPrcMore = oPrcOri - PriceRatioMore*oSpdD/100;
	End
	Else if oRocMa < 0 Then Begin
		oPrcLess = oPrcOri + PriceRatioLess*oSpdD/100;
		oPrcMid = oPrcOri + PriceRatioMid*oSpdD/100;
		oPrcMore = oPrcOri + PriceRatioMore*oSpdD/100;
	End;	

	return -1;
end;

{TODO: add dataN for TextLabel}
method int GetEnExitPrice()
variables: int dnum, int ret, string Sym, double PrcOri, int ShareTd, double SpdD, double PctChg, double PctSpd, double RocMa,
double PrcLess, double PrcMid, double PrcMore,
double PrcClose, double LastDClose, double HiD, double LoD;
begin
	
	Print("--Date--", "	", "-Time", "	", "Symbol", "	", "HighD", "	", "LowD", "	",
	"SpdD", "	", "PctChg", "	", "PctSpd", "	", "RocMa", "	", "-Price", "	", "Share", "	",
	Numtostr(PriceRatioLess, 0), "%- 	-",
	Numtostr(PriceRatioMid, 0), "%- 	-",
	Numtostr(PriceRatioMore, 0), "%-" );
	if TxtRegMktOpenGapR <> NULL Then
		TxtRegMktOpenGapR.TextString = 
		Numtostr(Time, 0) + ": " + Numtostr(Date, 0) + " $=" + Numtostr(CapAmt, 0);
		
	For dnum = 1 To DataNum Begin
		ret = GetEnExitPriceDataN(dnum, 1, Sym, PrcOri, HiD, LoD, SpdD, PctChg, PctSpd, RocMa,
			PrcLess, PrcMid, PrcMore);
		PrcLess = Round2Fraction(PrcLess);
		PrcMid = Round2Fraction(PrcMid);
		PrcMore = Round2Fraction(PrcMore);
		ShareTd = CapAmt/PrcOri;
	Print(Numtostr(Date,0), "	", Numtostr(Time,0), "	", Sym, "	", HiD, "	", LoD, "	", SpdD,
	"	[", PctChg, "	", PctSpd, "	", RocMa, "]	", PrcOri, "	[", Numtostr(ShareTd, 0),
	"]	", PrcLess,	"	", PrcMid, "	", PrcMore);
	
	if TxtRegMktOpenGapR <> NULL Then
		TxtRegMktOpenGapR.TextString = TxtRegMktOpenGapR.TextString	
		+ Newline + Sym + "=" + Numtostr(PrcOri, 2) + "[" + Numtostr(ShareTd, 0) + "] "
		+ ", " + Numtostr(PriceRatioLess, 0) + "%=" + Numtostr(PrcLess, 2)
		+ ", " + Numtostr(PriceRatioMid, 0) + "%=" + Numtostr(PrcMid, 2)
		+ ", " + Numtostr(PriceRatioMore, 0) + "%=" + Numtostr(PrcMore, 2);
	End;
	return -1;
end;

{Start to trade between TimeStartTrade and TimeOpen}
method bool IsTradeTime()
vars: bool isTime, double pctChg, double rngRb, double rngRd, double ret, int dnum;
begin
	if ( Time >= TimeStartTrade and Time < TimeOpen) Then
		isTime = True
	Else
		isTime = false;
	return isTime;
end;

once
begin
	HorizLineHi = CreateHorizontalLine(LineColorHi);
	HorizLineLo = CreateHorizontalLine(LineColorLo);
	HorizLine = CreateHorizontalLine(LineColor);
	if CurrentBar = 1 Then
		ClearPrintLog;
end;

if GF_FirstBarSession(TimeShowText) Then
Begin
	//DrawingObjects.Delete(TxtRegMktOpenGapR);
	Print(CurrentBar, ":", Time, "	TimeShowText=", TimeShowText);
	TxtRegMktOpenGapR = 
	TextLabel.Create(DTPoint.Create(BarDateTime[1], LowD(0)), //BNPoint.Create(CurrentBar, Low),
		Numtostr(CurrentBar, 0) + ":	" +  Numtostr(Time, 0) + " / " + Numtostr(OpenD(0), 2));
	
	DrawingObjects.Add(TxtRegMktOpenGapR);
End;

if IsTradeTime() Then
Begin
	GetEnExitPrice();
End;

{if ( BarType < 2 or BarType = 14 ) and 
 Date <> Date[1] and Currentbar > 1 then }
 if GF_FirstBarSession(TimeOpen) then
	begin
	//PlotPaintBar( High, Low, Open, Close, !( "FirstBar-D" ) ) ;
	
	GapR = GF_GapPercent(0);{100*(Open - Close[1])/Close[1];}
	BarRng = GF_GetBarRange(0);
	if(BarRng > 0) then
		SharesToTrade = GF_SharesToTrade(RiskAmt, BarRng);
	//Print("CurrentBar=" , Currentbar, ",Open=", Open, ",Close[1]=", Close[1], ",GapR=", GapR, ", BarRng=", BarRng);
	
{	TxtRegMktOpenGapR = 
	TextLabel.Create(BNPoint.Create(CurrentBar, Low),
		Numtostr(GapR, 2) + "%   " +  Numtostr(SharesToTrade, 0) + " / " + Numtostr(BarRng, 2));

	DrawingObjects.Add(TxtRegMktOpenGapR);}
	GetKeyRevSpread();
	GetOpenRngKeyRevSpdRatio();
	Print("KeyRevSpread, OpenRng, R = ", KeyRevSpread, BarRng, OpenRngR);
	Alert ;
	end ;

